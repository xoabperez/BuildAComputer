// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Inc16(in=PrevOut0, out=Plus1); //Add1
    Mux16(a=PrevOut0, b=Plus1, sel=inc, out=OutOrInc); // If inc, add 1
    Mux16(a=OutOrInc, b=in, sel=load, out=IncOrSet);   // If load, set to in
    Mux16(a=IncOrSet, b[0..15]=false, sel=reset, out=PrevOut); // If reset, 0
    Register(in=PrevOut, load=true, out=PrevOut0, out=out); // Save new prev out
}
